import com.diffplug.spotless.LineEnding

plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'com.diffplug.spotless' version "${spotless_version}"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

spotless {
	lineEndings = LineEnding.UNIX

	java {
		removeUnusedImports()
		importOrder('java', 'javax', 'net.minecraft', 'com.hezaerd')
		leadingSpacesToTabs()
		trimTrailingWhitespace()
	}

	// Format & sort lang files
	json {
		target 'src/**/lang/*.json'
		target 'src/**/generated/**'
		gson().indentWithSpaces(2).sortByKeys()
	}
}

fabricApi {
	configureDataGeneration {
		client = true
	}
}

repositories {
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	
	maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${project.devauth_version}")
	
	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	modImplementation "maven.modrinth:Wq5SjeWM:${project.fancy_menu_version}"
	modImplementation "maven.modrinth:J81TRJWm:${project.konkrete_version}"
	modImplementation "maven.modrinth:CVT4pFB2:${project.melody_version}"
}	

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}